10 RB=&H0106: ADDR=&H0104
20 B2R=&H038E: R2B=&H03A3
30 ST=&H8000
40 DIM X$(0)*24

50*DSP24: GOSUB *ADJUST
60 FOR J=0 TO 5
70 H$=HEX$(ST+J*4)+": "
80 T$="| "
90 X$(0)=""
100 FOR I=0 TO 3
110 A=PEEK(RB+J*4+I)
120 IF A<16 THEN
130 X$(0)=X$(0)+"0"
140 ENDIF
150 IF A<16 OR A>126 THEN
160 T$=T$+"."
170 ELSE
180 T$=T$+CHR$(A)
190 ENDIF

200 X$(0)=X$(0)+HEX$(A)+" "
210 NEXT
220 LOCATE 0,J: PRINT H$+X$(0)+T$
230 NEXT
240*WEHT00: H$=INKEY$
250 IF H$="" THEN *WEHT00
260 IF H$="Q" THEN *THEEND
270 IF H$="J" THEN
280 ST=ST+24
290 GOTO *DSP24
300 ENDIF
310 IF H$=CHR$(5) THEN
320 ST=ST+4
330 GOTO *DSP24
340 ENDIF
350 IF H$="K" THEN
360 ST=ST-24
370 IF ST<&H8000 THEN ST=&H8000
380 GOTO *DSP24
390 ENDIF
400 IF H$=CHR$(4) THEN
410 ST=ST-4
420 IF ST<&H8000 THEN ST=&H8000
430 GOTO *DSP24
440 ENDIF

450 IF H$="G" THEN
460 LOCATE 6,1: PRINT "____________"
470 LOCATE 6,2: PRINT "    GOTO    "
480 LOCATE 6,3: PRINT "            "
490 LOCATE 6,4: PRINT "____________"
500 LOCATE 7,3: INPUT "ADDR: ",H$
510 IF H$="" THEN *DSP24
520 WHILE LEN(H$)<4
530 H$="0"+H$
540 WEND
550 A=VAL("&H"+LEFT$(H$,2))
560 B=VAL("&H"+RIGHT$(H$,2))
570 A=A*256+B
580 IF A<&H8000 THEN *DSP24
590 IF A>&HBFE8 THEN *DSP24
600 ST=INT(A/16)
610 ST=ST*16
620 IF ST<&H8000 THEN ST=&H8000
630 GOTO *DSP24
640 ENDIF

650 IF H$="Z" THEN
660 LOCATE 6,1: PRINT "____________"
670 LOCATE 6,2: PRINT "  ZERO OUT  "
680 LOCATE 6,3: PRINT "            "
690 LOCATE 6,4: PRINT "____________"
700 LOCATE 6,3: INPUT " YOU SURE?",H$
710 IF H$<>"Y" THEN *DSP24
720 A=0: GOTO *FILLA
730 ENDIF

740 IF H$="F" THEN
750 LOCATE 6,1: PRINT "____________"
760 LOCATE 6,2: PRINT "  FILL OUT  "
770 LOCATE 6,3: PRINT "            "
780 LOCATE 6,4: PRINT "____________"
790 LOCATE 7,3: INPUT "VALUE: ",H$
800 IF H$="" THEN *DSP24
810 IF LEN(H$)>2 THEN *DSP24
820 A=VAL("&H"+H$)
830*FILLA: FOR I=0 TO 23
840 POKE (RB+I),A
850 NEXT
860 CALL R2B
870 GOTO *DSP24
880 ENDIF

890 GOTO *WEHT00

900*THEEND: END

910*ADJUST: A=INT(ST/256)
920 B=ST-(A*256)
930 POKE (ADDR),B
940 POKE (ADDR+1),A
950 CALL B2R
960 RETURN


