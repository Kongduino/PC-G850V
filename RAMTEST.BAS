10 RB=&H0106: ADDR=&H0104
20 B2R=&H038E: R2B=&H03A3
30 ST=&H8000
40 DIM X$(0)*24

50*DSP24: GOSUB *ADJUST
60 FOR J=0 TO 5
70 H$=HEX$(ST+J*4)+": "
80 T$="| "
90 X$(0)=""
100 FOR I=0 TO 3
110 A=PEEK(RB+J*4+I)
120 IF A<16 THEN
130 X$(0)=X$(0)+"0"
140 ENDIF
150 IF A<16 OR A>126 THEN
160 T$=T$+"."
170 ELSE
180 T$=T$+CHR$(A)
190 ENDIF

200 X$(0)=X$(0)+HEX$(A)+" "
210 NEXT
220 LOCATE 0,J: PRINT H$+X$(0)+T$
230 NEXT
240*WEHT00: H$=INKEY$
250 IF H$="" THEN *WEHT00
260 IF H$="Q" THEN *THEEND
270 IF H$="J" THEN
280 ST=ST+24
290 GOTO *DSP24
300 ENDIF
310 IF H$=CHR$(5) THEN
320 ST=ST+4
330 GOTO *DSP24
340 ENDIF
350 IF H$="K" THEN
360 ST=ST-24
370 IF ST<&H8000 THEN ST=&H8000
380 GOTO *DSP24
390 ENDIF
400 IF H$=CHR$(4) THEN
410 ST=ST-4
420 IF ST<&H8000 THEN ST=&H8000
430 GOTO *DSP24
440 ENDIF

450 IF H$="G" THEN
460 LOCATE 6,1: PRINT "____________"
470 LOCATE 6,2: PRINT "    GOTO    "
480 LOCATE 6,3: PRINT "            "
490 LOCATE 6,4: PRINT "____________"
500 LOCATE 7,3: INPUT "ADDR: ",H$
510 IF H$="" THEN *DSP24
520 WHILE LEN(H$)<4
530 H$="0"+H$
540 WEND
550 A=VAL("&H"+LEFT$(H$,2))
560 B=VAL("&H"+RIGHT$(H$,2))
570 A=A*256+B
580 IF A<&H8000 THEN *DSP24
590 IF A>&HBFE8 THEN *DSP24
600 ST=INT(A/16)
610 ST=ST*16
620 IF ST<&H8000 THEN ST=&H8000
630 GOTO *DSP24
640 ENDIF

650 IF H$="Z" THEN
660 LOCATE 6,1: PRINT "____________"
670 LOCATE 6,2: PRINT "  ZERO OUT  "
680 LOCATE 6,3: PRINT "            "
690 LOCATE 6,4: PRINT "____________"
700 LOCATE 6,3: INPUT " YOU SURE?",H$
710 IF H$<>"Y" THEN *DSP24
720 A=0: GOTO *FILLA
730 ENDIF

740 IF H$="S" THEN
750 LOCATE 6,1: PRINT "____________"
760 LOCATE 6,2: PRINT "   STRING   "
770 LOCATE 6,3: PRINT "            "
780 LOCATE 6,4: PRINT "____________"
790 LOCATE 6,3: INPUT " FILL?",X$(0)
800 IF X$(0)="" THEN *DSP24
810 J=LEN(X$(0))
820 IF J>23 THEN J=23
830 FOR I=1 TO J
840 POKE (RB+I-1),ASC(MID$(X$(0),I,1))
850 NEXT
860 POKE(RB+I-1),0
870 CALL R2B
880 GOTO *DSP24
890 ENDIF

900 IF H$="F" THEN
910 LOCATE 6,1: PRINT "____________"
920 LOCATE 6,2: PRINT "  FILL OUT  "
930 LOCATE 6,3: PRINT "            "
940 LOCATE 6,4: PRINT "____________"
950 LOCATE 7,3: INPUT "VALUE: ",H$
960 IF H$="" THEN *DSP24
970 IF LEN(H$)>2 THEN *DSP24
980 A=VAL("&H"+H$)
990*FILLA: FOR I=0 TO 23
1000 POKE (RB+I),A
1010 NEXT
1020 CALL R2B
1030 GOTO *DSP24
1040 ENDIF

1050 IF H$="H" THEN
1060 CLS
1070 PRINT "J: DOWN 24   : UP 24"
1080 PRINT "K: UP 24     : UP  4"
1090 PRINT "Z: ZERO OUT F: FILL"
1100 PRINT "S: STRING   G: GO ADDR"
1105 PRINT "H: HELP...  Q: QUIT"
1110 PRINT "PRESS SPACE TO CONTINUE"
1120 GCURSOR(70,15)
1130 GPRINT "04027F0204"
1140 GCURSOR(70,7)
1150 GPRINT "10207F2010"
1160*HELP: H$=INKEY$
1170 IF H$=" " THEN *DSP24
1180 GOTO *HELP
1190 ENDIF

1200 GOTO *WEHT00

1210*THEEND: END

1220*ADJUST: A=INT(ST/256)
1230 B=ST-(A*256)
1240 POKE (ADDR),B
1250 POKE (ADDR+1),A
1260 CALL B2R
1270 RETURN

