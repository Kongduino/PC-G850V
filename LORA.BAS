10 OPEN "COM1:": PRINT #1,"AT"
20 DIM X$(0)*64
30 FRQ=433
40 BW=1
50 SF=12
60 CR=1
70 PWR=20
80 SENDER$="R"
90 GOSUB "LOADSETTINGS"
100 REM if there's no settings, at least defaults are set
110 DIM CMD$(0)*64
120 GOSUB "SENDATSETTINGS"

130*INIT: CLS : PRINT "   - LoRa Messenger -"
140 PRINT "[s]end    se[t]tings"
150 PRINT "[r]eceive [q]uit"

160*MAINLOOP: A$= INKEY$
170 IF A$="S" THEN "SENDTEXT"
180 IF A$="Q" THEN "BAIL"
190 IF A$="T" THEN "SETTINGS"
200 IF A$="R" THEN
210 GOSUB "CLEARBOTTOMHALF"
220 LOCATE 0,3: PRINT "Rx: "
230 INPUT # 1,X$(0)
240 LOCATE 0,4: PRINT X$(0)
250 CLOSE : OPEN "COM:"
260 ENDIF
270 GOTO "MAINLOOP"

280*SENDTEXT: GOSUB "CLEARBOTTOMHALF"
290 LOCATE 0,3: INPUT "Text: "; X$(0)
300 IF SENDER$="R" THEN
310 REM For use with RAK Firmware
320 GOSUB "PREPAREATCMD"
330 PRINT #1,"at+send=lorap2p:"+CMD$(0)
340 ELSE
350 REM Or to send just what was entered, just do
360 PRINT #1,X$(0)
370 ENDIF
380 GOTO "MAINLOOP"

390*SETTINGS: CLS : PRINT "      SETTINGS"
400 IF SENDER$="R" THEN
410 PRINT "*[R]AK [F]req [S]F [P]wr"
420 PRINT " [P]lain [B]W [C]R"
430 ELSE
440 PRINT " [R]AK [F]req [S]F [P]wr"
450 PRINT "*[P]lain [B]W [C]R"
460 ENDIF
470 PRINT "Send init [A]T cmd [e]nd"
480*SETTINGSLOOP: A$= INKEY$
490 IF A$="P" THEN
500 SENDER$="P"
510 GOTO "SETTINGS"
520 ENDIF

530 IF A$="R" THEN
540 SENDER$="R"
550 GOTO "SETTINGS"
560 ENDIF

570 IF A$="F" THEN
580 GOSUB "CLEARBOTTOMHALF"
590 LOCATE 0,4: INPUT "Freq: "; F$
600 F= VAL (F$)
610 IF (F>0) THEN
620 FRQ=F
630 ENDIF
640 GOTO "SETTINGS"
650 ENDIF

660 IF A$="E" THEN
670 WAIT (1*64)
680 LOCATE 0,5: PRINT "end of settings"
690 WAIT (0)
700 GOSUB "SAVESETTINGS"
710 GOTO "INIT"
720 ENDIF

730 IF A$="A" THEN
740 GOSUB "SENDATSETTINGS"
750 WAIT (3*64)
760 LOCATE 0,5: PRINT CMD$(0)
770 WAIT (0)
780 GOTO "SETTINGS"
790 ENDIF

800 IF A$="C" THEN
810 LOCATE 0,4: PRINT "C/R 4/[5678]"
820*CRLOOP: B$= INKEY$
830 IF B$="" THEN "CRLOOP"
840 F= VAL (B$)
850 IF F<5 OR F>8 THEN "CRLOOP"
860 CR=F-4
870 GOTO "SETTINGS"
880 ENDIF

890 IF A$="S" THEN
900 GOSUB "CLEARBOTTOMHALF"
910 LOCATE 0,4: INPUT "SF [6-12]: "; F
920 IF F<6 OR F>12 THEN "SETTINGSLOOP"
930 SF=F
940 GOTO "SETTINGS"
950 ENDIF

960 IF A$="B" THEN
970 LOCATE 0,4: PRINT "BW 125/250/500 [012]"
980*BWLOOP: B$= INKEY$
990 IF B$="" THEN "BWLOOP"
1000 IF B$<"0" OR B$>"2" THEN "BWLOOP"
1010 BW= VAL (B$)
1020 GOTO "SETTINGS"
1030 ENDIF

1040 GOTO "SETTINGSLOOP"

1050*CLEARBOTTOMHALF: LINE (0,24)-(143,47),R,BF
1060 RETURN

1070*PREPAREATCMD: J= LEN (X$(0)): CMD$(0)=""
1080 FOR I=1 TO J
1090 Y$= MID$ (X$(0),I,1): N= ASC (Y$)
1100 M= INT (N/16)
1110 IF M<10 THEN
1120 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1130 ELSE
1140 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1150 ENDIF
1160 M=N-(M*16)
1170 IF M<10 THEN
1180 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1190 ELSE
1200 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1210 ENDIF
1220 NEXT I
1230 RETURN

1240*SENDATSETTINGS: CMD$(0)="at+set_config=lorap2p:"+ STR$ (FRQ*1000000)+":"
1250 CMD$(0)=CMD$(0)+ STR$ (SF)+":"+ STR$ (BW)+":"+ STR$ (CR)+":8:"+ STR$ (PWR)+""
1260 PRINT #1, CMD$(0)
1270 RETURN

1280*LOADSETTINGS: OPEN "E:LORA.TXT" FOR INPUT AS #2
1290*LOOPLOADS LNINPUT #2, A$
1300 REM PRINT "Dealing with "+A$
1310 IF EOF (2) THEN
1320 CLOSE #2
1330 RETURN
1340 ENDIF
1350 C$= LEFT$ (A$,2)

1360 IF C$="SD" THEN
1370 SENDER$= MID$ (A$,3,3): GOTO "LOOPLOADS"
1380 ENDIF

1390 V= VAL ( MID$ (A$,3,3))
1400 IF C$="FQ" THEN
1410 FRQ=V
1420 ENDIF

1430 IF C$="CR" THEN
1440 CR=V
1450 ENDIF

1460 IF C$="BW" THEN
1470 BW=V
1480 ENDIF

1490 IF C$="SF" THEN
1500 SF=V
1510 ENDIF

1520 IF C$="PW" THEN
1530 PW=V
1540 ENDIF

1550 GOTO "LOOPLOADS"

1560*SAVESETTINGS: OPEN "E:LORA.TXT" FOR OUTPUT AS #2
1570 PRINT #2, "FQ"+ STR$ (FRQ)
1580 PRINT #2, "CR"+ STR$ (CR)
1590 PRINT #2, "BW"+ STR$ (BW)
1600 PRINT #2, "SF"+ STR$ (SF)
1610 PRINT #2, "PW"+ STR$ (PW)
1620 PRINT #2, "SD"+SENDER$
1630 CLOSE #2
1640 RETURN

2000*BAIL: CLS : PRINT "Good bye..."
2010 CLOSE
2020 END

