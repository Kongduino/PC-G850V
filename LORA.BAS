10 OPEN "COM1:": PRINT #1,"AT"
20 DIM X$(0)*64
30 FRQ=433
40 BW=1
50 SF=12
60 CR=1
65 PWR=20
70 SENDER$="R"
75 GOSUB "LOADSETTINGS"
76 REM if there's no settings, at least defaults are set
80 DIM CMD$(0)*64
85 GOSUB "SENDATSETTINGS"

90*INIT: CLS: PRINT "   - LoRa Messenger -"
100 PRINT "[s]end    se[t]tings"
110 PRINT "[r]eceive [q]uit"
120*MAINLOOP: A$= INKEY$
130 IF A$="S" THEN "SENDTEXT"
140 IF A$="Q" THEN "BAIL"
150 IF A$="T" THEN "SETTINGS"
160 IF A$="R" THEN
170 GOSUB "CLEARBOTTOMHALF"
180 LOCATE 0,3: PRINT "Rx: "
190 INPUT # 1,X$(0)
200 LOCATE 0,4: PRINT X$(0)
210 CLOSE: OPEN "COM:"
220 ENDIF
230 GOTO "MAINLOOP"

240*SENDTEXT: GOSUB "CLEARBOTTOMHALF"
250 LOCATE 0,3: INPUT "Text: "; X$(0)
260 IF SENDER$="R" THEN
270 REM For use with RAK Firmware
280 GOSUB "PREPAREATCMD"
290 PRINT #1,"at+send=lorap2p:"+CMD$(0)
300 ELSE
310 REM Or to send just what was entered, just do
320 PRINT #1,X$(0)
330 ENDIF
340 GOTO "MAINLOOP"

350*SETTINGS: CLS: PRINT "      SETTINGS"
360 IF SENDER$="R" THEN
370 PRINT "*[R]AK [F]req [S]F [P]wr"
380 PRINT " [P]lain [B]W [C]R"
390 ELSE
400 PRINT " [R]AK [F]req [S]F [P]wr"
410 PRINT "*[P]lain [B]W [C]R"
420 ENDIF
430 PRINT "Send init [A]T cmd [e]nd"
440*SETTINGSLOOP: A$= INKEY$
450 IF A$="P" THEN
460 SENDER$="P"
470 GOTO "SETTINGS"
480 ENDIF

490 IF A$="R" THEN
500 SENDER$="R"
510 GOTO "SETTINGS"
520 ENDIF

530 IF A$="F" THEN
540 GOSUB "CLEARBOTTOMHALF"
550 LOCATE 0,4: INPUT "Freq: "; F$
560 F= VAL (F$)
570 IF (F>0) THEN
580 FRQ=F
590 ENDIF
600 GOTO "SETTINGS"
610 ENDIF

620 IF A$="E" THEN
621 WAIT (1*64)
622 LOCATE 0,5: PRINT "end of settings"
623 WAIT (0)
625 GOSUB "SAVESETTINGS"
630 GOTO "INIT"
640 ENDIF

650 IF A$="A" THEN
660 GOSUB "SENDATSETTINGS"
670 WAIT (3*64)
680 LOCATE 0,5: PRINT CMD$(0)
690 WAIT (0)
700 GOTO "SETTINGS"
710 ENDIF

740 IF A$="C" THEN
750 LOCATE 0,4: PRINT "C/R 4/[5678]"
760*CRLOOP: B$= INKEY$
770 IF B$="" THEN "CRLOOP"
780 F= VAL (B$)
790 IF F<5 OR F>8 THEN "CRLOOP"
800 CR=F-4
810 GOTO "SETTINGS"
820 ENDIF

830 IF A$="S" THEN
840 GOSUB "CLEARBOTTOMHALF"
850 LOCATE 0,4: INPUT "SF [6-12]: "; F
860 IF F<6 OR F>12 THEN "SETTINGSLOOP"
870 SF=F
880 GOTO "SETTINGS"
890 ENDIF

900 IF A$="B" THEN
910 LOCATE 0,4: PRINT "BW 125/250/500 [012]"
920*BWLOOP: B$= INKEY$
930 IF B$="" THEN "BWLOOP"
940 IF B$<"0" OR B$>"2" THEN "BWLOOP"
950 BW=VAL (B$)
960 GOTO "SETTINGS"
970 ENDIF

980 GOTO "SETTINGSLOOP"

990*CLEARBOTTOMHALF: LINE (0,24)-(143,47),R,BF
1000 RETURN

1010*PREPAREATCMD: J= LEN (X$(0)): CMD$(0)=""
1020 FOR I=1 TO J
1030 Y$= MID$ (X$(0),I,1): N= ASC (Y$)
1040 M= INT (N/16)
1050 IF M<10 THEN
1060 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1070 ELSE
1080 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1090 ENDIF
1100 M=N-(M*16)
1110 IF M<10 THEN
1120 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1130 ELSE
1140 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1150 ENDIF
1160 NEXT I
1170 RETURN

1200*SENDATSETTINGS: CMD$(0)="at+set_config=lorap2p:"+STR$(FRQ*1000000)+":"
1210 CMD$(0)=CMD$(0)+ STR$(SF)+":"+ STR$(BW)+":"+ STR$(CR)+":8:"+STR$(PWR)+""
1220 PRINT #1, CMD$(0)
1230 RETURN

1300*LOADSETTINGS: OPEN "E:LORA.TXT" FOR INPUT AS #2
1310*LOOPLOADS LNINPUT #2, A$
1312 REM PRINT "Dealing with "+A$
1320 IF EOF(2) THEN
1322 CLOSE #2
1324 RETURN
1326 ENDIF
1330 C$=LEFT$(A$,2)
1340 IF C$="SD" THEN
1350 SENDER$=MID$(A$,3,3): GOTO "LOOPLOADS"
1360 ENDIF
1370 V=VAL(MID$(A$,3,3))
1380 IF C$="FQ" THEN
1390 FRQ=V
1400 ENDIF
1410 IF C$="CR" THEN
1420 CR=V
1430 ENDIF
1440 IF C$="BW" THEN
1450 BW=V
1460 ENDIF
1470 IF C$="SF" THEN
1480 SF=V
1490 ENDIF
1500 IF C$="PW" THEN
1510 PW=V
1520 ENDIF
1530 GOTO "LOOPLOADS"

1900*SAVESETTINGS: OPEN "E:LORA.TXT" FOR OUTPUT AS #2
1910 PRINT #2, "FQ"+STR$(FRQ)
1920 PRINT #2, "CR"+STR$(CR)
1930 PRINT #2, "BW"+STR$(BW)
1940 PRINT #2, "SF"+STR$(SF)
1950 PRINT #2, "PW"+STR$(PW)
1960 PRINT #2, "SD"+SENDER$
1970 CLOSE #2
1980 RETURN

2000*BAIL: CLS: PRINT "Good bye..."
2010 CLOSE
2020 END

