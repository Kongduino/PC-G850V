10 CALL 256
20 OPEN "COM1:"
30 DIM X$(0)*64: FQ=433
40 BW=1
50 SF=12
60 CR=1
70 PWR=20
80 SENDER$="R"
90 GOSUB "LOADSETTINGS"
100 REM if there's no settings, at least defaults are set
110 DIM CMD$(0)*64
120 GOSUB "SENDATSETTINGS"


200*INIT: CALL 256
210 LOCATE 0,0:PRINT "LoRa Messenger"
220 PRINT "[Z] [X] [*] [Q]"

230*MAINLOOP: A$= INKEY$
240 IF A$="X" THEN "SENDTEXT"
250 IF A$="Q" THEN "BAIL"
260 IF A$="*" THEN "SETTINGS"
270 IF A$="Z" THEN "RECEIVE"
280 GOTO "MAINLOOP"


300*RECEIVE: GOSUB "CLEARBOTTOMHALF"
310 LOCATE 0,3: PRINT "Rx: "
320 INPUT # 1,X$(0)
330 LOCATE 0,4: PRINT X$(0)
340 CLOSE : OPEN "COM:"
350 REM prevents I/O errors
360*RCVLOOP: A$= INKEY$
370 IF A$="" THEN "RCVLOOP"
380 GOTO "INIT"
390 REM wait for key press


400*SENDTEXT: GOSUB "CLEARBOTTOMHALF"
410 LOCATE 0,3: INPUT "Text: "; X$(0)
420 IF SENDER$="R" THEN
430 REM For use with RAK Firmware
440 GOSUB "PREPAREATCMD"
450 PRINT #1,"at+send=lorap2p:"+CMD$(0)
460 ELSE
470 PRINT #1,X$(0)
480 ENDIF
490 GOTO "INIT"

500*SETTINGS: CLS : PRINT " SETTINGS"
510 IF SENDER$="R" THEN
520 PRINT "*[R]AK [F]req [S]F P[w]r"
530 PRINT " [P]lain [B]W [C]R [e]nd"
540 ELSE
550 PRINT " [R]AK [F]req [S]F [P]wr"
560 PRINT "*[P]lain [B]W [C]R [e]nd"
570 ENDIF

580*SETTINGSLOOP: A$= INKEY$
590 IF A$="P" THEN
600 SENDER$="P"
610 GOTO "SETTINGS"
620 ENDIF
630 IF A$="R" THEN
640 SENDER$="R"
650 GOTO "SETTINGS"
660 ENDIF
670 IF A$="F" THEN
680 GOSUB "CLEARBOTTOMHALF"
690 LOCATE 0,4: INPUT "Freq: "; F
700 IF F>115 AND F<1000 THEN
710 FQ=F
720 ENDIF
730 GOTO "SETTINGS"
740 ENDIF
750 IF A$="W" THEN
760 GOSUB "CLEARBOTTOMHALF"
770 LOCATE 0,4: INPUT "Power: "; F
780 IF F>4 AND F<21 THEN
790 PWR=F
800 ENDIF
810 GOTO "SETTINGS"
820 ENDIF
830 IF A$="E" THEN
840 IF SENDER$="R" THEN GOSUB "SENDATSETTINGS"
850 GOSUB "CLEARBOTTOMHALF"
860 REM WAIT (3*64)
870 LOCATE 0,4: PRINT CMD$(0)
880 GOSUB "SAVESETTINGS"
890 WAIT (0)
900 GOTO "INIT"
910 ENDIF
920 IF A$="C" THEN
930 LOCATE 0,4: PRINT "C/R 4/[5678]"

940*CRLOOP: B$= INKEY$
950 IF B$="" THEN "CRLOOP"
960 F= VAL (B$)
970 IF F<5 OR F>8 THEN "CRLOOP"
980 CR=F-4
990 GOTO "SETTINGS"
1000 ENDIF
1010 IF A$="S" THEN
1020 GOSUB "CLEARBOTTOMHALF"
1030 LOCATE 0,4: INPUT "SF [6-12]: "; F
1040 IF F<6 OR F>12 THEN "SETTINGSLOOP"
1050 SF=F
1060 GOTO "SETTINGS"
1070 ENDIF
1080 IF A$="B" THEN
1090 LOCATE 0,4: PRINT "BW 125/250/500 [012]"

1100*BWLOOP: B$= INKEY$
1110 IF B$="" THEN "BWLOOP"
1120 IF B$<"0" OR B$>"2" THEN "BWLOOP"
1130 BW= VAL (B$)
1140 GOTO "SETTINGS"
1150 ENDIF
1160 GOTO "SETTINGSLOOP"

1170*CLEARBOTTOMHALF: LINE (0,24)-(143,47),R,BF
1180 RETURN

1190*PREPAREATCMD: J= LEN (X$(0)): CMD$(0)=""
1200 FOR I=1 TO J
1210 Y$= MID$ (X$(0),I,1): N= ASC (Y$)
1220 M= INT (N/16)
1230 IF M<10 THEN
1240 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1250 ELSE
1260 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1270 ENDIF
1280 M=N-(M*16)
1290 IF M<10 THEN
1300 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1310 ELSE
1320 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1330 ENDIF
1340 NEXT I
1350 RETURN

1360*SENDATSETTINGS: CMD$(0)="at+set_config=lorap2p:"+ STR$ (FQ*1000000)+":"
1370 CMD$(0)=CMD$(0)+ STR$ (SF)+":"+ STR$ (BW)+":"+ STR$ (CR)+":8:"+ STR$ (PWR)+""
1380 PRINT #1, CMD$(0)
1390 RETURN

1400*LOADSETTINGS: OPEN "E:LORA.TXT" FOR INPUT AS #2
1410 *LOOPLOADS LNINPUT #2, X$(0)
1420 PRINT #1, "Dealing with "+X$(0)
1430 IF EOF (2) THEN
1440 CLOSE #2
1450 RETURN
1460 ENDIF
1470 C$= LEFT$ (X$(0),2)
1480 IF C$="SD" THEN
1490 SENDER$= MID$(X$(0),3,1): GOTO "LOOPLOADS"
1500 ENDIF
1510 V= VAL(MID$ (X$(0),3,7))
1520 IF C$="FQ" THEN
1530 FQ=V
1540 ENDIF
1550 IF C$="CR" THEN
1560 CR=V
1570 ENDIF
1580 IF C$="BW" THEN
1590 BW=V
1600 ENDIF
1610 IF C$="SF" THEN
1620 SF=V
1630 ENDIF
1640 IF C$="PW" THEN
1650 PW=V
1660 ENDIF
1670 GOTO "LOOPLOADS"

1680*SAVESETTINGS: OPEN "E:LORA.TXT" FOR OUTPUT AS #2
1690 PRINT #2, "FQ"+ STR$ (FQ)
1700 PRINT #2, "CR"+ STR$ (CR)
1710 PRINT #2, "BW"+ STR$ (BW)
1720 PRINT #2, "SF"+ STR$ (SF)
1730 PRINT #2, "PW"+ STR$ (PW)
1740 PRINT #2, "SD"+SENDER$
1750 CLOSE #2
1760 RETURN


2000*BAIL: CLS : PRINT "Good bye..."
2100 CLOSE
2200 END

