10 OPEN "COM1:"
20 DIM X$(0)*64
30 FRQ=433
40 BW=1
50 SF=12
60 CR=1
70 PWR=20
80 SENDER$="R"
90 GOSUB "LOADSETTINGS"
100 REM if there's no settings, at least defaults are set
110 DIM CMD$(0)*64
120 GOSUB "SENDATSETTINGS"

130*INIT: CLS : PRINT "   - LoRa Messenger -"
140 PRINT "[s]end    se[t]tings"
150 PRINT "[r]eceive [q]uit"

160*MAINLOOP: A$= INKEY$
170 IF A$="S" THEN "SENDTEXT"
180 IF A$="Q" THEN "BAIL"
190 IF A$="T" THEN "SETTINGS"
200 IF A$="R" THEN
210 GOSUB "CLEARBOTTOMHALF"
220 LOCATE 0,3: PRINT "Rx: "
230 INPUT # 1,X$(0)
240 LOCATE 0,4: PRINT X$(0)
250 REM CLOSE : OPEN "COM:"
252 REM COM1: is full duplex.
254 REM No CLOSE/OPEN needed.
256 REM I TINK TO.
260 ENDIF
270 GOTO "MAINLOOP"

280*SENDTEXT: GOSUB "CLEARBOTTOMHALF"
290 LOCATE 0,3: INPUT "Text: "; X$(0)
300 IF SENDER$="R" THEN
310 REM For use with RAK Firmware
320 GOSUB "PREPAREATCMD"
330 PRINT #1,"at+send=lorap2p:"+CMD$(0)
340 ELSE
350 REM Or to send just what was entered, just do
360 PRINT #1,X$(0)
370 ENDIF
380 GOTO "MAINLOOP"

390*SETTINGS: CLS : PRINT "      SETTINGS"
400 IF SENDER$="R" THEN
410 PRINT "*[R]AK [F]req [S]F P[w]r"
420 PRINT " [P]lain [B]W [C]R [e]nd"
430 ELSE
440 PRINT " [R]AK [F]req [S]F [P]wr"
450 PRINT "*[P]lain [B]W [C]R [e]nd"
460 ENDIF

480*SETTINGSLOOP: A$= INKEY$
490 IF A$="P" THEN
500 SENDER$="P"
510 GOTO "SETTINGS"
520 ENDIF

530 IF A$="R" THEN
540 SENDER$="R"
550 GOTO "SETTINGS"
560 ENDIF

570 IF A$="F" THEN
580 GOSUB "CLEARBOTTOMHALF"
590 LOCATE 0,4: INPUT "Freq: "; F
600 IF F>115 AND F<1000 THEN
610 FRQ=F
620 ENDIF
630 GOTO "SETTINGS"
640 ENDIF

650 IF A$="W" THEN
660 GOSUB "CLEARBOTTOMHALF"
670 LOCATE 0,4: INPUT "Power: "; F
680 IF F>4 AND F<21 THEN
690 PWR=F
700 ENDIF
710 GOTO "SETTINGS"

720 ENDIF

730 IF A$="E" THEN
740 GOSUB "SENDATSETTINGS"
750 GOSUB "CLEARBOTTOMHALF"
760 WAIT (3*64)
770 LOCATE 0,4: PRINT CMD$(0)
780 GOSUB "SAVESETTINGS"
790 WAIT (0)
800 GOTO "INIT"
810 ENDIF

870 IF A$="C" THEN
880 LOCATE 0,4: PRINT "C/R 4/[5678]"
890*CRLOOP: B$= INKEY$
900 IF B$="" THEN "CRLOOP"
910 F= VAL (B$)
920 IF F<5 OR F>8 THEN "CRLOOP"
930 CR=F-4
940 GOTO "SETTINGS"
950 ENDIF

960 IF A$="S" THEN
970 GOSUB "CLEARBOTTOMHALF"
980 LOCATE 0,4: INPUT "SF [6-12]: "; F
990 IF F<6 OR F>12 THEN "SETTINGSLOOP"
1000 SF=F
1010 GOTO "SETTINGS"
1020 ENDIF

1030 IF A$="B" THEN
1040 LOCATE 0,4: PRINT "BW 125/250/500 [012]"
1050*BWLOOP: B$= INKEY$
1060 IF B$="" THEN "BWLOOP"
1070 IF B$<"0" OR B$>"2" THEN "BWLOOP"
1080 BW= VAL (B$)
1090 GOTO "SETTINGS"
1100 ENDIF
1110 GOTO "SETTINGSLOOP"

1120*CLEARBOTTOMHALF: LINE (0,24)-(143,47),R,BF
1130 RETURN

1140*PREPAREATCMD: J= LEN (X$(0)): CMD$(0)=""
1150 FOR I=1 TO J
1160 Y$= MID$ (X$(0),I,1): N= ASC (Y$)
1170 M= INT (N/16)
1180 IF M<10 THEN
1190 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1200 ELSE
1210 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1220 ENDIF
1230 M=N-(M*16)
1240 IF M<10 THEN
1250 CMD$(0)=CMD$(0)+ CHR$ (M+48)
1260 ELSE
1270 CMD$(0)=CMD$(0)+ CHR$ (M+55)
1280 ENDIF
1290 NEXT I
1300 RETURN

1310*SENDATSETTINGS: CMD$(0)="at+set_config=lorap2p:"+ STR$ (FRQ*1000000)+":"
1320 CMD$(0)=CMD$(0)+ STR$ (SF)+":"+ STR$ (BW)+":"+ STR$ (CR)+":8:"+ STR$ (PWR)+""
1330 PRINT #1, CMD$(0)
1340 RETURN

1350*LOADSETTINGS: OPEN "E:LORA.TXT" FOR INPUT AS #2
1360*LOOPLOADS LNINPUT #2, X$(0)
1370 REM PRINT "Dealing with "+X$(0)
1380 IF EOF (2) THEN
1390 CLOSE #2
1400 RETURN
1410 ENDIF
1420 C$= LEFT$ (X$(0),2)

1430 IF C$="SD" THEN
1440 SENDER$= MID$(X$(0),3,1): GOTO "LOOPLOADS"
1450 ENDIF
1460 V= VAL(MID$ (X$(0),3,7))
1470 IF C$="FQ" THEN
1480 FRQ=V
1490 ENDIF

1500 IF C$="CR" THEN
1510 CR=V
1520 ENDIF

1530 IF C$="BW" THEN
1540 BW=V
1550 ENDIF

1560 IF C$="SF" THEN
1570 SF=V
1580 ENDIF

1590 IF C$="PW" THEN
1600 PW=V
1610 ENDIF
1620 GOTO "LOOPLOADS"

1630*SAVESETTINGS: OPEN "E:LORA.TXT" FOR OUTPUT AS #2
1640 PRINT #2, "FQ"+ STR$ (FRQ)
1650 PRINT #2, "CR"+ STR$ (CR)
1660 PRINT #2, "BW"+ STR$ (BW)
1670 PRINT #2, "SF"+ STR$ (SF)
1680 PRINT #2, "PW"+ STR$ (PW)
1690 PRINT #2, "SD"+SENDER$
1700 CLOSE #2
1710 RETURN

2000*BAIL: CLS : PRINT "Good bye..."
2100 CLOSE
2200 END

