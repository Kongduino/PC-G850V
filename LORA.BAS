10 OPEN "COM:": PRINT #1,"KONGDUINO"
20 DIM X$(0)*64
30 SENDER$="R"
40 DIM CMD$(0)*64
50*INIT: CLS : PRINT "   - LoRa Messenger -"
60 PRINT "[s]end se[t]tings [e]nd"
70 PRINT "[r]eceive"
80*MAINLOOP: A$= INKEY$
90 IF A$="S" THEN "SENDTEXT"
100 IF A$="E" THEN "BAIL"
110 IF A$="T" THEN "SETTINGS"
120 IF A$="R" THEN
130 GOSUB "CLEARBOTTOMHALF"
140 LOCATE 0,3: PRINT "Rx: "
150 INPUT # 1,X$(0)
160 LOCATE 0,4: PRINT X$(0)
170 CLOSE : OPEN "COM:"
180 ENDIF
190 GOTO "MAINLOOP"

200*SENDTEXT: GOSUB "CLEARBOTTOMHALF"
210 LOCATE 0,3: INPUT "Text: "; X$(0)
205 IF SENDER$="R" THEN
206 REM For use with RAK Firmware
220 GOSUB "PREPAREATCMD"
240 PRINT #1,"at+send=lorap2p:"+CMD$(0)
242 ELSE
250 REM Or to send just what was entered, just do
260 PRINT #1,X$(0)
262 ENDIF
270 GOTO "MAINLOOP"

280*SETTINGS: CLS : PRINT "      SETTINGS"
290 IF SENDER$="R" THEN
300 PRINT "*[R]AK Firmware"
310 PRINT " [P]lain transfer [e]nd"
320 ELSE
330 PRINT " [R]AK Firmware"
340 PRINT "*[P]lain transfer [e]nd"
350 ENDIF
360*SETTINGSLOOP: A$= INKEY$
370 IF A$="P" THEN
380 SENDER$="P"
390 GOTO "SETTINGS"
400 ENDIF
410 IF A$="R" THEN
420 SENDER$="R"
430 GOTO "SETTINGS"
440 ENDIF
450 IF A$="E" THEN
460 PRINT "LEAVING SETTINGS"
465 WAIT(5)
470 GOTO "INIT"
480 ENDIF
490 GOTO "SETTINGSLOOP"

500*CLEARBOTTOMHALF: LINE (0,24)-(143,47),R,BF
510 RETURN
520*PREPAREATCMD: J= LEN (X$(0)): CMD$(0)=""
530 FOR I=1 TO J
540 Y$= MID$ (X$(0),I,1): N= ASC (Y$)
550 M= INT (N/16)
560 IF M<10 THEN
570 CMD$(0)=CMD$(0)+ CHR$ (M+48)
580 ELSE
590 CMD$(0)=CMD$(0)+ CHR$ (M+55)
600 ENDIF
610 M=N-(M*16)
620 IF M<10 THEN
630 CMD$(0)=CMD$(0)+ CHR$ (M+48)
640 ELSE
650 CMD$(0)=CMD$(0)+ CHR$ (M+55)
660 ENDIF
670 NEXT I
680 RETURN
1000*BAIL: CLS : PRINT "Good bye..."
1010 CLOSE
1020 END

