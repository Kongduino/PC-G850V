10 OPEN "COM:": PRINT #1,"KONGDUINO"
20 DIM X$(0)*64
30 FRQ=433
40 BW=1
50 SF=12
60 CR=1
70 SENDER$="R"
80 DIM CMD$(0)*64
90*INIT: CLS : PRINT "   - LoRa Messenger -"
100 PRINT "[s]end    se[t]tings"
110 PRINT "[r]eceive [e]nd"
120*MAINLOOP: A$= INKEY$
130 IF A$="S" THEN "SENDTEXT"
140 IF A$="E" THEN "BAIL"
150 IF A$="T" THEN "SETTINGS"
160 IF A$="R" THEN
170 GOSUB "CLEARBOTTOMHALF"
180 LOCATE 0,3: PRINT "Rx: "
190 INPUT # 1,X$(0)
200 LOCATE 0,4: PRINT X$(0)
210 CLOSE : OPEN "COM:"
220 ENDIF
230 GOTO "MAINLOOP"
240*SENDTEXT: GOSUB "CLEARBOTTOMHALF"
250 LOCATE 0,3: INPUT "Text: "; X$(0)
260 IF SENDER$="R" THEN
270 REM For use with RAK Firmware
280 GOSUB "PREPAREATCMD"
290 PRINT #1,"at+send=lorap2p:"+CMD$(0)
300 ELSE
310 REM Or to send just what was entered, just do
320 PRINT #1,X$(0)
330 ENDIF
340 GOTO "MAINLOOP"
350*SETTINGS: CLS : PRINT "      SETTINGS"
360 IF SENDER$="R" THEN
370 PRINT "*[R]AK [F]req [S]F"
380 PRINT " [P]lain [B]W [C]R"
390 ELSE
400 PRINT " [R]AK [F]req [S]F"
410 PRINT "*[P]lain [B]W [C]R"
420 ENDIF
430 PRINT "Send init [A]T cmd [e]nd"
440*SETTINGSLOOP: A$= INKEY$
450 IF A$="P" THEN
460 SENDER$="P"
470 GOTO "SETTINGS"
480 ENDIF

490 IF A$="R" THEN
500 SENDER$="R"
510 GOTO "SETTINGS"
520 ENDIF

530 IF A$="F" THEN
540 GOSUB "CLEARBOTTOMHALF"
550 LOCATE 0,4: INPUT "Freq: "; F$
560 F= VAL (F$)
570 IF (F>0) THEN
580 FRQ=F
590 ENDIF
600 GOTO "SETTINGS"
610 ENDIF

620 IF A$="E" THEN
630 GOTO "INIT"
640 ENDIF

650 IF A$="A" THEN
660 CMD$(0)="at+set_config=lorap2p:"+ STR$ (FRQ*1000000)+":"
670 CMD$(0)=CMD$(0)+ STR$ (SF)+":"+ STR$ (BW)+":"+ STR$ (CR)+":8:20"
680 PRINT #1, CMD$(0)
690 WAIT (3*64)
700 LOCATE 0,5: PRINT CMD$(0)
710 WAIT (0)
720 GOTO "SETTINGS"
730 ENDIF

740 IF A$="C" THEN
741 LOCATE 0,4: PRINT "C/R 4/[5678]"
742*CRLOOP B$=INKEY$
743 IF B$="" THEN "CRLOOP"
744 F=VAL(B$)
745 IF F<5 OR F>8 THEN "CRLOOP"
746 CR=F-4
747 GOTO "SETTINGS"
748 ENDIF

750 IF A$="S" THEN
751 LOCATE 0,4: PRINT "SF [6-12]"
752 INPUT F
754 IF F<6 OR F>12 THEN "SETTINGSLOOP"
756 SF=F
757 GOTO "SETTINGS"
758 ENDIF

760 IF A$="B" THEN
761 LOCATE 0,4: PRINT "BW 125/250/500 [012]"
762*BWLOOP B$=INKEY$
763 IF B$="" THEN "BWLOOP"
765 IF B$<"0" OR B$>"2" THEN "BWLOOP"
766 BW=VAL(B$)
767 GOTO "SETTINGS"
768 ENDIF

799 GOTO "SETTINGSLOOP" 


810*CLEARBOTTOMHALF: LINE (0,24)-(143,47),R,BF
820 RETURN
830*PREPAREATCMD: J= LEN (X$(0)): CMD$(0)=""
840 FOR I=1 TO J
850 Y$= MID$ (X$(0),I,1): N= ASC (Y$)
860 M= INT (N/16)
870 IF M<10 THEN
880 CMD$(0)=CMD$(0)+ CHR$ (M+48)
890 ELSE
900 CMD$(0)=CMD$(0)+ CHR$ (M+55)
910 ENDIF
920 M=N-(M*16)
930 IF M<10 THEN
940 CMD$(0)=CMD$(0)+ CHR$ (M+48)
950 ELSE
960 CMD$(0)=CMD$(0)+ CHR$ (M+55)
970 ENDIF
980 NEXT I
990 RETURN
1000*BAIL: CLS : PRINT "Good bye..."
1010 CLOSE
1020 END

