10 CALL 256 ; asm raklog2 first!
20 OPEN "COM1:": PRINT #1, CHR$(10)+CHR$(10)+"LS"
30 CS=&H0100
40 CE=PEEK(&H7FFF)*256+PEEK(&H7FFE)-1
50 LOCATE 0,0: PRINT "Code: "
60 A$=RIGHT$("000"+HEX$(CS),4)+ "-"
70 B$=RIGHT$("000"+HEX$(CE),4)
80 DIM S$(0)*12
90 LOCATE 7,0: PRINT A$
100 LOCATE 12,0: PRINT B$
110 PRINT #1, "Code:  "+A$+ "-"+B$
120 DS=CE+1
130 DE=PEEK(&H7974)*256+PEEK(&H7973)-1
140 LOCATE 0,1: PRINT "RAMD:"
150 A$=RIGHT$("000"+HEX$(DS),4)+ "-"
160 B$=RIGHT$("000"+HEX$(DE),4)
170 LOCATE 7,1: PRINT A$
180 LOCATE 12,1: PRINT B$
190 PRINT #1, "RAMD:  "+A$+ "-"+B$
200 TS=DE+1
210 TE=PEEK(&H7976)*256+PEEK(&H7975)
220 A$=RIGHT$("000"+HEX$(TS),4)+ "-"
230 B$=RIGHT$("000"+HEX$(TE),4)
240 PRINT #1, "Text:  "+A$+B$
250 LOCATE 0,2: PRINT "Text:"
260 LOCATE 7,2: PRINT A$
270 LOCATE 12,2: PRINT B$
280 BS=PEEK(&H79E2)*256+PEEK(&H79E1)
290 BE=PEEK(&H79E4)*256+PEEK(&H79E3)
300 A$=RIGHT$("000"+HEX$(BS),4)+ "-"
310 B$=RIGHT$("000"+HEX$(BE),4)
320 PRINT #1, "BASIC: "+A$+B$
330 LOCATE 0,3: PRINT "BASIC:"
340 LOCATE 7,3: PRINT RIGHT$("000"+HEX$(BS),4)+ "-"
350 LOCATE 12,3: PRINT RIGHT$("000"+HEX$(BE),4)
360 GOSUB "WAITK"
370 FE=PEEK(DS+19)*256+PEEK(DS+18)+DS
380 C=0
390 WHILE FE<DE
400 FT=PEEK(FE)
410 N=PEEK(FE+18)*256+PEEK(FE+17)
420 N=N+FE
430 S$(0)=""
440 P$=""
450 FOR I=1 TO 8
460 X=PEEK(FE+I)
470 IF X>32 THEN
480 S$(0)=S$(0)+CHR$(X)
490 ELSE
500 P$=P$+" "
510 ENDIF
520 NEXT
530 S$(0)=S$(0)+"."
540 FOR I=9 TO 11
550 X=PEEK(FE+I)
560 IF X>32 THEN
570 S$(0)=S$(0)+CHR$(X)
580 ENDIF
590 NEXT
600 PRINT S$(0)+P$+" ("+HEX$(FT)+") "+STR$(N-FE-34)
610 PRINT #1,HEX$(FE)+" "+S$(0)+P$+" ("+HEX$(FT)+") "+STR$(N-FE-34)
620 FE=N
630 C=C+1
640 IF C=6 THEN
650 GOSUB "WAITK"
660 C=0
670 ENDIF
680 WEND

999 END
1000*WAITK: A$=INKEY$
1010 IF A$="" THEN "WAITK"
1020 RETURN

